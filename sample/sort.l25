program Sort{
    func sort(st: int*, ed: int*, cmp: func bool(int, int)): void{
        if (ed - st > 1) {
            let mid = st + (ed - st) / 2;
            sort(st, mid, cmp);
            sort(mid, ed, cmp);
            let buf = int*(alloc(ed - st));
            {
                let i = st;
                let j = mid;
                let k = buf;
                while (i != mid || j != ed) {
                    if (j == ed || (i != mid && cmp(*i, *j))) {
                        *k = *i;
                        i = i + 1;
                        k = k + 1;
                    } else {
                        *k = *j;
                        j = j + 1;
                        k = k + 1;
                    };
                };
            };
            {
                let i = st;
                let j = buf;
                while (i != ed) {
                    *i = *j;
                    i = i + 1;
                    j = j + 1;
                };
            };
            free(buf);
        };
        return default;
    }

    func cmp(x: int, y: int): bool{
        let res = x > y;
        return res;
    }

    main{
        let n: int;
        input(n);
        let a = int*(alloc(n));
        {
            let i = 0;
            while (i < n) {
                input(a[i]);
                i = i + 1;
            };
        };
        sort(a, a + n, cmp);
        {
            let i = 0;
            while (i < n) {
                output(a[i], ' ');
                i = i + 1;
            };
        };
        free(a);
        output('\n');
    }
}

// 输入：
// 5
// 1 3 5 2 4
// 输出：
// 5 4 3 2 1
